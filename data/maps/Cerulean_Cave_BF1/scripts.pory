raw`
Cerulean_Cave_BF1_MapScripts::

`

script Cerulean_Cave_BF1_Mewtwo_Trigger {
	removeobject(1)
	playmoncry(SPECIES_MEWTWO, CRY_MODE_NORMAL)
	waitmoncry
	setvar(VAR_MEWTWO_TRIGGER, 1)
}

script Cerulean_Cave_BF1_EventScript_Mewtwo {
	lock
	setvar(VAR_MEWTWO_TRIGGER, 0)
	setvar (VAR_0x8004, 1)  // vertical pan
	setvar (VAR_0x8005, 1)  // horizontal pan
	setvar (VAR_0x8006, 9)  // num shakes
	setvar (VAR_0x8007, 7 ) // shake delay
	special (ShakeCamera)
	playmoncry(SPECIES_MEWTWO, CRY_MODE_NORMAL)
	waitmoncry
	fadescreen(FADE_TO_BLACK)
	delay(2)
	playse (SE_BANG)
	addobject(1)
	delay(2)
	fadescreen(FADE_FROM_BLACK)
	delay(30)
	special (ShakeCamera)
	playmoncry(SPECIES_MEWTWO, CRY_MODE_ENCOUNTER)
	waitmoncry
	delay(35)
	applymovement(LOCALID_MEWTWO, MewtwoEngage)
	waitmovement
	delay(25)
	setflag(FLAG_CANT_FLEE)
	setwildbattle(SPECIES_MEWTWO, 70, ITEM_NONE)
	special(BattleSetup_StartLegendaryBattle)
	specialvar(VAR_RESULT, GetBattleOutcome)
	waitstate
	if(var(VAR_RESULT) == B_OUTCOME_CAUGHT){
		call(Won_or_Caught_Mewtwo)
	}
	elif(var(VAR_RESULT) == B_OUTCOME_WON){
		call(Won_or_Caught_Mewtwo)
	}
	else{
		call(RanAway_Mewtwo)
	}
}
 
script Won_or_Caught_Mewtwo {
	setvar(VAR_MEWTWO_TRIGGER, 2)
	clearflag(FLAG_CANT_FLEE)
	fadescreen(FADE_TO_BLACK)
	removeobject(1)
	fadescreen(FADE_FROM_BLACK)
	end
}

script RanAway_Mewtwo {
	end
}

movement MewtwoEngage {
	walk_down
}